set lmargin 7
set rmargin 2
set tmargin 2
set bmargin 3

unset key

set size ratio 1
set size 0.5

set term po co eps enhanced font "TimesNewRoman"
set output "tmp.eps"

set multiplot
dx=0.26
dy=0.37

set grid

black="#000000"
col00="#0000ff"        
col01="#0080ff"
col02="#00ffff"
col03="#00ff80"
col04="#00ff00"
col05="#80ff00"
col06="#ffff00"
col07="#ff8000"
col08="#ff0000"

#============================================
#============================================

set origin dx*0, dy*1

set xrange [-0.45:0.45]
set xtic -0.6, 0.2, 0.6
set mxtic 4
set format x ""

set yrange [0.05:1.15]
set ytic -10, 0.2, 10
set mytic 4
set ylabel "{/Symbol r}"

pl \
"off/sph_t0007.dat" u 3:16 pt 6 ps 0.5 lc 7, \
"sph_t0007.dat"     u 3:16 pt 6 ps 0.5 lc 1, \
"shock_sod.dat"     u ($1-0.5):3 w l lt 1 lc 3 lw 2

#============================================

set origin dx*1, dy*1

set yrange [0.05:1.15]
set mytic 4

set format y ""
set ylabel ""

set y2range [0.05:1.15]
set y2tic -10, 0.2, 10
set my2tic 4
set format y2 "%g"
set y2label "P"

pl \
"off/sph_t0007.dat" u 3:18 pt 6 ps 0.5 lc 7, \
"sph_t0007.dat"     u 3:18 pt 6 ps 0.5 lc 1, \
"shock_sod.dat"     u ($1-0.5):4 w l lt 1 lc 3 lw 2

#============================================
#============================================

set origin dx*0, dy*0

set format x "%g"
set xlabel "x"

set yrange [-0.1:0.7]
set mytic 4
set format y "%g"
set ylabel "v_x"

set y2range [-0.1:0.7]
set format y2 ""
set y2label ""

pl \
"off/sph_t0007.dat" u 3:6 pt 6 ps 0.5 lc 7, \
"sph_t0007.dat"     u 3:6 pt 6 ps 0.5 lc 1, \
"shock_sod.dat"     u ($1-0.5):2 w l lt 1 lc 3 lw 2

#============================================

set origin dx*1, dy*0

set yrange [1:1.2]
set ytic 1, 0.05, 1.2
set mytic 5

set format y ""
set ylabel ""

set y2range [1:1.2]
set y2tic 1, 0.05, 1.2
set my2tic 5
set format y2 "%g"
set y2label "c_s"

pl \
"off/sph_t0007.dat" u 3:17 pt 6 ps 0.5 lc 7, \
"sph_t0007.dat"     u 3:17 pt 6 ps 0.5 lc 1, \
"shock_sod.dat" u ($1-0.5):5 w l lt 1 lc 3 lw 2
