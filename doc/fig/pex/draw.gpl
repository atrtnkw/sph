set lmargin 7
set rmargin 2
set tmargin 2
set bmargin 3

unset key

set size ratio 1
set size 0.5

set term po co eps enhanced font "TimesNewRoman"
set output "tmp.eps"

set multiplot
dx=0.26
dy=0.37

set grid

black="#000000"
col00="#0000ff"        
col01="#0080ff"
col02="#00ffff"
col03="#00ff80"
col04="#00ff00"
col05="#80ff00"
col06="#ffff00"
col07="#ff8000"
col08="#ff0000"

#============================================
#============================================

set origin dx*0, dy*1

set xrange [0:0.5]
set xtic 0, 0.1, 0.5
set mxtic 4
set format x ""

set yrange [0:5]
set ytic -10, 1, 10
set mytic 4
set ylabel "{/Symbol r}"

pl \
"sph_t0005.dat" u (sqrt($3**2+$4**2+$5**2)):16 w d

#============================================

set origin dx*1, dy*1

set yrange [0:10]
set ytic -10, 2, 10
set mytic 4
set format y ""
set ylabel ""

set y2range [0:10]
set y2tic -10, 2, 10
set my2tic 4
set format y2 "%g"
set y2label "P"

pl \
"sph_t0005.dat" u (sqrt($3**2+$4**2+$5**2)):18 w d

#============================================
#============================================

set origin dx*0, dy*0

set format x "%g"
set xlabel "x"

set yrange [0:3]
set ytic -10, 0.4, 10
set mytic 4
set format y "%g"
set ylabel "v_x"

set y2range [0:3]
set y2tic -10, 0.4, 10
set my2tic 4
set format y2 ""
set y2label ""

pl \
"sph_t0005.dat" \
u (sqrt($3**2+$4**2+$5**2)):(($3*$6+$4*$7+$5*$8)/sqrt($3**2+$4**2+$5**2)) w d

#============================================

set origin dx*1, dy*0

set yrange [0:20]
set ytic 0, 4, 20
set mytic 4
set format y ""
set ylabel ""

set y2range [0:20]
set y2tic 0, 4, 20
set my2tic 4
set format y2 "%g"
set y2label "u"

pl \
"sph_t0005.dat" u (sqrt($3**2+$4**2+$5**2)):12 w d
